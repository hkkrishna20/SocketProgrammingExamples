buildscript {
    ext {
        springBootVersion = "1.5.8.RELEASE"
        lombokVersion = "1.16.16"
        slf4jVersion = "1.7.25"
        logbackVersion = "1.2.3"
        couchbaseClientVersion = "2.5.0"
        couchbaseCoreIOVersion = "1.5.0"
        rxjavaVersion = "1.3.2"
        hystrixVersion = "1.5.12"
        mockitoVersion = "2.11.0"
        assertjVersion = "3.8.0"
        gradlePluginVersion = "3.2.0"
        dependencyManagementPluginVersion = "1.0.3.RELEASE"
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath(
                "com.bmuschko:gradle-docker-plugin:$gradlePluginVersion",
                "io.spring.gradle:dependency-management-plugin:$dependencyManagementPluginVersion",
                "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        )
    }
}

subprojects { subproject ->
    if (subproject.name == "couchbase-client") {
        apply plugin: "org.springframework.boot"
        apply plugin: "com.bmuschko.docker-remote-api"
    }
}

allprojects {
    if (!project.plugins.hasPlugin("io.spring.dependency-management")) {
        apply plugin: "io.spring.dependency-management"
    }

    apply plugin: "java"

    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.8

    repositories {
        jcenter()
    }

    dependencyManagement {
        dependencies {
            dependency "io.reactivex:rxjava:$rxjavaVersion"
            dependency "org.mockito:mockito-core:$mockitoVersion"
            dependency "org.assertj:assertj-core:$assertjVersion"
        }
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
        }
    }

    dependencies {
        compileOnly(
                "org.projectlombok:lombok:$lombokVersion"
        )
        compile(
                "org.springframework.boot:spring-boot-starter-web",
                "com.couchbase.client:core-io:$couchbaseCoreIOVersion",
                "com.couchbase.client:java-client:$couchbaseClientVersion"
        )
        runtime(
                "org.slf4j:slf4j-api:$slf4jVersion",
                "ch.qos.logback:logback-classic:$logbackVersion",
                "ch.qos.logback:logback-core:$logbackVersion"
        )
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

project(":couchbase-lib") {
    dependencies {
        compile(
                "com.netflix.hystrix:hystrix-core:$hystrixVersion"
        )
    }
}

project(":couchbase-client") {
    bootRun {
        systemProperties = System.properties
    }

    dependencies {
        compile project(':couchbase-lib')
    }

//    task createDockerfile(type: Dockerfile) {
//        dependsOn bootRepackage
//
//        destFile = new File(buildDir, "Dockerfile")
//        from("openjdk:8u131-jdk-alpine")
//
//        def sourceJar = "libs/${project.name}-${project.version}.jar"
//        def destJar = "/app.jar"
//
//        copyFile(sourceJar, destJar)
//
//        runCommand("touch $destJar")
//
//        entryPoint("java", "-Djava.security.egd=file:/dev/./urandom", "-jar", destJar)
//    }

//    task copyDockerfile(type: Copy) {
//        from fileTree("docker").files
//        into buildDir
//    }
//
//    task buildImage(type: DockerBuildImage, dependsOn: [bootRepackage, copyDockerfile]) {
//        inputDir = buildDir
//        tag = "asarkar/${project.name}"
//        remove = true
//    }
}

