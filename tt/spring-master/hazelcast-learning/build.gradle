plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.50"
    id "idea"
    id "org.springframework.boot" version "2.1.7.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

group = "org.asarkar"
version = "1.0-SNAPSHOT"
// Kotlin generates Java 8 byte code
java.sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
            "org.springframework.boot:spring-boot-starter-web",
            "javax.cache:cache-api:$jCacheApiVersion"
    )
    implementation(
            "com.hazelcast:hazelcast-client:$hazelcastVersion",
            "com.hazelcast:hazelcast:$hazelcastVersion"
    )
    runtimeOnly(
            "com.hazelcast:hazelcast-kubernetes:$hazelcastK8SVersion",
            "org.jetbrains.kotlin:kotlin-reflect",
            "com.fasterxml.jackson.module:jackson-module-kotlin",
            "org.springframework.boot:spring-boot-starter-cache"
    )
    testImplementation(
            "org.junit.jupiter:junit-jupiter-api",
            "com.willowtreeapps.assertk:assertk-jvm:$assertkVersion"
    )
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "junit", module: "junit"
        exclude group: "org.assertj", module: "assertj-core"
        exclude group: "org.hamcrest", module: "hamcrest-library"
        exclude group: "org.hamcrest", module: "hamcrest-core"
        exclude group: "org.mockito", module: "mockito-core"
    }
    testRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine"
    )
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "skipped", "failed"]
        exceptionFormat = "short"
        showCauses = true
        showExceptions = true
        showStackTraces = true
        displayGranularity = 2
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        // Kotlin generates Java 8 byte code
        jvmTarget = "1.8"
    }
}